import Materiales.*
import Experimentos.*
import Personajes.*
import Estrategias.*
import Acciones.*

describe "Tests Rick" {
	const lata10 = new Lata(10)
	const lata250 = new Lata(250)
	const cable2000x2 = new Cable(2000,2)
	const cable3000x2 = new Cable(3000,2)
	const fleeb = new Fleeb(lata10,16)
	const materiaOscura = new MateriaOscura(cable3000x2)
	
	fixture {
		rick.asignarCompanero(morty)
		morty.recuperarEnergia(280)
		morty.recolectar(lata250) //Para construirBateria y construirCircuito
		morty.recolectar(cable2000x2) //Para construirCircuito
		morty.recolectar(materiaOscura) //Para construirCircuito
		morty.darObjetosA(rick)
		
		morty.recolectar(fleeb) //Para construirBateria
		morty.darObjetosA(rick)
    }
	
	test "Mochila" {
		assert.equals(#{ lata250, cable2000x2, materiaOscura, fleeb },rick.mochila())
	}
	
	test "Experimentos para Realizar1 (construirBateria)" {
		rick.mochila().add(new Bateria([lata250,fleeb]))
		assert.equals(#{ construirBateria,shockElectrico, construirCircuito },rick.experimentosQuePuedeRealizar())
	}
	test "Experimentos para Realizar2 (shockElectrico)" {
		rick.realizar(construirBateria)
		assert.equals(#{ construirBateria, construirCircuito, shockElectrico },rick.experimentosQuePuedeRealizar())
	}
	test "Hacer Experimento (mochila)" {
		const mochilaSobrante = #{ cable2000x2, materiaOscura }
		rick.realizar(construirBateria)
		assert.equals(mochilaSobrante,rick.mochila().intersection(mochilaSobrante))
		assert.equals(3,rick.mochila().size())
	}
	test "Hacer Experimento (compa√±ero)" {
		//En este punto Morty tiene 20 de energia
		rick.mochila().clear()
		rick.mochila().add(new Bateria(#{lata10,fleeb})) //generaE = grMetal(10) * 2
		rick.mochila().add(lata10) //conduceE = 0.1 * _grMetal(10)
		rick.realizar(shockElectrico)
		assert.equals((20+40),rick.companiero().energia())
	}
	
	test "Test contruir una bateria con estrategia de menorCantidadDeMetal"{
		
		rick.estrategia(menorCantidadDeMetal) //por defecto elige al azar
		rick.mochila().clear()
		var lata333 = new Lata(333)
		var lata350 = new Lata(350)

		rick.mochila().add(lata333)
		rick.mochila().add(lata350)
		rick.mochila().add(lata250)
		rick.mochila().add(lata10)
		rick.mochila().add(fleeb)
		rick.realizar(construirBateria)
		
		assert.notThat(rick.mochila().contains(lata250))
			
	}
	
	test "Test contruir un shockElectrico con estrategia mejorGeneradorElectrico" {
		
		rick.estrategia(mejorGeneradorElectrico) //por defecto elige al azar
		rick.mochila().clear()
		
		var parasitoIncrementaEnergia = new ParasitoAlienigena(new IncrementarEnergia(1)) //genera 5
		var materiaGenera10 = new MateriaOscura(parasitoIncrementaEnergia) // genera el doble de su materiaBase 
		
		rick.mochila().add(parasitoIncrementaEnergia)
		rick.mochila().add(materiaGenera10)
		rick.mochila().add(cable3000x2)
		
		rick.realizar(shockElectrico)
		
		assert.notThat(rick.mochila().contains(materiaGenera10))
		assert.that(rick.mochila().contains(parasitoIncrementaEnergia))
	}
	
	test "Test contruir un circuito con la estrategia ecologica " {
		
		rick.estrategia(ecologico) //por defecto elige al azar
		rick.mochila().clear()
		
		var fleebConduce25 = new Fleeb(lata250,19)
		
		rick.mochila().add(fleebConduce25) //conduce y esta vivo
		rick.mochila().add(lata250) // conduce 25
		rick.mochila().add(cable3000x2) //conduce 6

		
		rick.realizar(construirCircuito)
		
		assert.that(rick.mochila().contains(cable3000x2))
		assert.that(rick.mochila().contains(lata250))
		assert.notThat(rick.mochila().contains(fleebConduce25))	

	}
}
	